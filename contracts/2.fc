{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}

#include "imports/stdlib.fc";

int tlen (tuple t) asm "TLEN";

() recv_internal() {
}

;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id {
    int n = matrixA.tlen();
    int m = matrixB.tlen();
    int p = matrixB.first().tlen();

    tuple result = empty_tuple();

    int i_n = 0;
    while (i_n < n) {
        tuple new_row = empty_tuple();

        int i_p = 0;
        while (i_p < p) {
            int value = 0;
            int i_m = 0;
            while (i_m < m) {
                value += matrixA.at(i_n).at(i_m) * matrixB.at(i_m).at(i_p);
                i_m += 1;
            }
            new_row~tpush(value);
            i_p += 1;
        }
        result~tpush(new_row);
        i_n += 1;
    }
    return result;
}


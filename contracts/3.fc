{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

(int) tlen (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

() recv_internal() {
}

int int_len(int n) inline {
    int len = 0;
    while (n) {
        n = n >> 1;
        len += 1;
    }
    return len;
}

(int, int) break_int(int n, int int_len, int left_len, int right_len) inline {
    cell dc = begin_cell().store_uint(n, int_len).end_cell();
    slice ds = dc.begin_parse();
    return (ds~load_uint(left_len), ds~load_uint(right_len));
}

(builder, (tuple, int)) save_bits(builder b, tuple builders, int loaded_bits, int value, int value_len) inline {
    if (value_len) {
        if (loaded_bits + value_len <= 1023) {
            b~store_uint(value, value_len);
            loaded_bits += value_len;
        } else {
            int left_len = (1023 - loaded_bits);
            int right_len = (value_len - left_len);
            (int left, int right) = break_int(value, value_len, left_len, right_len);
            b~store_uint(left, left_len);

            builders~tpush(b);
            b = begin_cell();
            b~store_uint(right, right_len);
            loaded_bits = right_len;
        }
    }
    return (b, (builders, loaded_bits));
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
    tuple builders = empty_tuple();
    builder cur_builder = begin_cell();

    slice ds = linked_list.begin_parse();

    int flag_len = int_len(flag);
    int value_len = int_len(value);

    slice next_slice = begin_cell().end_cell().begin_parse();

    int loaded_bits = 0;
    int has_ref = 1;
    while (has_ref > 0) {
        int bits = ds.slice_bits();
        while (bits >= flag_len) {
            if (flag == ds.preload_uint(flag_len)) {
                ds~skip_bits(flag_len);
                (builders, loaded_bits) = cur_builder~save_bits(builders, loaded_bits, value, value_len);
                bits -= flag_len;
            } else {
                cur_builder~store_uint(ds~load_uint(1), 1);
                loaded_bits += 1;
                bits -= 1;
            }
            if (loaded_bits == 1023) {
                builders~tpush(cur_builder);
                cur_builder = begin_cell();
                loaded_bits = 0;
            }
        }

        if (next_slice.slice_data_empty?()) {
            if (ds.slice_refs_empty?()) {
                (builders, _) = cur_builder~save_bits(builders, loaded_bits, ds~load_uint(bits), bits);
                if (cur_builder.builder_bits()) {
                    builders~tpush(cur_builder);
                }
                has_ref = 0;
            } else {
                slice new_ds = ds~load_ref().begin_parse();
                int new_bits_len = min(1023 - bits, new_ds.slice_bits());
                ds = begin_cell()
                        .store_uint(ds~load_uint(bits), bits)
                        .store_uint(new_ds~load_uint(new_bits_len), new_bits_len)
                        .end_cell()
                        .begin_parse();
                next_slice = new_ds;
            }
        } else {
            int next_slice_len = next_slice.slice_bits();
            ds = begin_cell()
                    .store_uint(ds~load_uint(bits), bits)
                    .store_uint(next_slice~load_uint(next_slice_len), next_slice_len)
                    .end_cell()
                    .begin_parse();
        }

        ;;         if (ds.slice_refs_empty?()) {
        ;;             if (next_slice.slice_data_empty?()) {
        ;;                 (builders, _) = cur_builder~save_bits(builders, loaded_bits, ds~load_uint(bits), bits);
        ;;                 if (cur_builder.builder_bits()) {
        ;;                     builders~tpush(cur_builder);
        ;;                 }
        ;;                 has_ref = 0;
        ;;             } else {
        ;;                 int next_slice_len = next_slice.slice_bits();
        ;;                 ds = begin_cell()
        ;;                         .store_uint(ds~load_uint(bits), bits)
        ;;                         .store_uint(next_slice~load_uint(next_slice_len), next_slice_len)
        ;;                         .end_cell()
        ;;                         .begin_parse();
        ;;             }
        ;;         } else {
        ;;             slice new_ds = ds~load_ref().begin_parse();
        ;;             int next_slice_len = next_slice.slice_bits();
        ;;             int new_bits_len = min(1023 - bits - next_slice_len, new_ds.slice_bits());
        ;;             ds = begin_cell()
        ;;                     .store_uint(ds~load_uint(bits), bits)
        ;;                     .store_uint(next_slice~load_uint(next_slice_len), next_slice_len)
        ;;                     .store_uint(new_ds~load_uint(new_bits_len), new_bits_len)
        ;;                     .end_cell()
        ;;                     .begin_parse();
        ;;             next_slice = new_ds;
        ;;         }
    }

    builder value = builders~tpop();
    cell dc = value.end_cell();

    int count = builders.tlen();
    repeat (count) {
        value = builders~tpop();
        dc = value.store_ref(dc).end_cell();
    }

    return dc;
}


{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}


int tlen (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

int pow2 (int n) asm "POW2";
int bit_size(int n) asm "UBITSIZE";
(int, int) bit_refs(slice s) asm "SBITREFS";
(slice, int) is_begin_with(slice s, slice sub) asm "SDBEGINSXQ";

() recv_internal() {
}

(builder, (tuple, int)) save_bits(builder b, tuple builders, int loaded_bits, int value, int value_len, int cell_max_bit) inline {
    if (loaded_bits + value_len <= cell_max_bit) {
        b~store_uint(value, value_len);
        loaded_bits += value_len;
        if (loaded_bits == cell_max_bit) {
            builders~tpush(b);
            b = begin_cell();
            loaded_bits = 0;
        }
    } else {
        int left_len = cell_max_bit - loaded_bits;
        int right_len = value_len - left_len;
        b~store_uint(value >> right_len, left_len);
        builders~tpush(b);

        b = begin_cell().store_uint(value & (pow2(right_len) - 1), right_len);
        loaded_bits = right_len;
    }
    return (b, (builders, loaded_bits));
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {

    if ((flag == value) | (flag == 0) | (value == 0)) {
        return linked_list;
    }

    slice ds = linked_list.begin_parse();
    if (ds.slice_data_empty?()) {
        return linked_list;
    }

    tuple builders = empty_tuple();
    builder cur_builder = begin_cell();

    int cell_max_bit = 1023;

    int flag_len = bit_size(flag);
    int flag_len_sub_1 = flag_len  - 1;
    int flag_mask = pow2(flag_len) - 1;

    int value_len = bit_size(value);

    slice flag_slice = cur_builder
            .store_uint(flag, flag_len)
            .end_cell()
            .begin_parse();


    if (flag_len == value_len) {

        tuple values = empty_tuple();
        tuple masks = empty_tuple();

        int shift = 256 - flag_len;
        repeat (256 - flag_len + 1) {
            values~tpush(flag << shift);
            masks~tpush(flag_mask << shift);
            shift -= 1;
        }
        int masks_len = values.tlen();

        int has_ref = 1;
        while (has_ref > 0) {
            int remained_bits = ds.slice_bits();

            int can_load = 0;
            while (ds.slice_bits() >= 256) { ;; change
                int data = ds.preload_uint(256);
                int n = 0;
                while (n < masks_len) {
                    if (data & masks.at(n) == values.at(n)) {
                        cur_builder~store_uint(ds~load_uint(can_load), can_load);
                        cur_builder~store_uint(value, value_len);
                        ds~skip_bits(flag_len);
                        n += flag_len;
                        can_load = 0;
                    } else {
                        ;;                                                 int store = ds~load_uint(1);
                        ;;                                                 cur_builder~store_uint(store, 1);
                        n += 1;
                        can_load += 1;
                    }
                }
                cur_builder~store_uint(ds~load_uint(can_load), can_load);
                can_load = 0;
            }
            remained_bits = ds.slice_bits();

            while (remained_bits >= flag_len) {
                (ds, int success) = is_begin_with(ds, flag_slice);
                if (success) {
                    cur_builder~store_uint(value, value_len);
                    remained_bits -= flag_len;
                } else {
                    cur_builder~store_uint(ds~load_uint(1), 1);
                    remained_bits -= 1;
                }
            }

            if (ds.slice_refs_empty?()) {
                if (remained_bits) {
                    cur_builder~store_uint(ds~load_uint(remained_bits), remained_bits);
                }
                builders~tpush(cur_builder);
                has_ref = 0;
            }  else {
                if (remained_bits > 0) {
                    int left = ds~load_uint(remained_bits);
                    ds = ds~load_ref().begin_parse();
                    int right_len = min(flag_len_sub_1, ds.slice_bits());

                    int right = ds.preload_uint(right_len);
                    int segment_len = remained_bits + right_len;

                    int segment = (left << right_len) + right;
                    if (segment_len >= flag_len) {
                        int shift = segment_len - flag_len;
                        int break = 0;
                        do {
                            if (((segment >> shift) & flag_mask) == flag) {
                                break = 1;
                            } else {
                                shift -= 1;
                            }
                        } until ((shift < 0) | break)
                        if (shift >= 0) {
                            int skip_right = right_len - shift;
                            ds~skip_bits(skip_right);
                            int left_replaced = shift + flag_len - right_len;
                            int save_value = ((left >> left_replaced) << value_len) + value;
                            int save_value_len = remained_bits - left_replaced + value_len;

                            cur_builder~store_uint(save_value >> (save_value_len - remained_bits), remained_bits);
                            builders~tpush(cur_builder);
                            cur_builder = begin_cell();
                            cur_builder~store_uint(save_value & (pow2(skip_right) - 1), skip_right);
                        }  else {
                            cur_builder~store_uint(left, remained_bits);
                            builders~tpush(cur_builder);
                            cur_builder = begin_cell();
                        }
                    } else {
                        cur_builder~store_uint(left, remained_bits);
                        builders~tpush(cur_builder);
                        cur_builder = begin_cell();
                    }
                } else {
                    ds = ds~load_ref().begin_parse();
                    builders~tpush(cur_builder);
                    cur_builder = begin_cell();
                }
            }
        }

    } else {


        int loaded_bits = 0;
        int has_ref = 1;
        while (has_ref > 0) {
            int remained_bits = ds.slice_bits();

            while (remained_bits >= flag_len) {
                (ds, int success) = is_begin_with(ds, flag_slice);
                if (success) {
                    (builders, loaded_bits) = cur_builder~save_bits(builders, loaded_bits, value, value_len, cell_max_bit);
                    remained_bits -= flag_len;
                } else {
                    cur_builder~store_uint(ds~load_uint(1), 1);
                    loaded_bits += 1;
                    remained_bits -= 1;
                }
                if (loaded_bits == cell_max_bit) {
                    builders~tpush(cur_builder);
                    cur_builder = begin_cell();
                    loaded_bits = 0;
                }
            }

            if (ds.slice_refs_empty?()) {
                if (remained_bits) {
                    (builders, _) = cur_builder~save_bits(builders, loaded_bits, ds~load_uint(remained_bits), remained_bits, cell_max_bit);
                }
                if (cur_builder.builder_bits()) {
                    builders~tpush(cur_builder);
                }
                has_ref = 0;
            }  else {
                if (remained_bits > 0) {
                    int left = ds~load_uint(remained_bits);
                    ds = ds~load_ref().begin_parse();
                    int right_len = min(flag_len_sub_1, ds.slice_bits());

                    int right = ds.preload_uint(right_len);
                    int segment_len = remained_bits + right_len;

                    int segment = (left << right_len) + right;
                    if (segment_len >= flag_len) {
                        int shift = segment_len - flag_len;
                        int break = 0;
                        do {
                            if (((segment >> shift) & flag_mask) == flag) {
                                break = 1;
                            } else {
                                shift -= 1;
                            }
                        } until ((shift < 0) | break)
                        if (shift >= 0) {
                            ds~skip_bits(right_len - shift);
                            int left_replaced = shift + flag_len - right_len;
                            (builders, loaded_bits) = cur_builder~save_bits(builders, loaded_bits, ((left >> left_replaced) << value_len) + value, remained_bits - left_replaced + value_len, cell_max_bit);
                        }  else {
                            (builders, loaded_bits) = cur_builder~save_bits(builders, loaded_bits, left, remained_bits, cell_max_bit);
                        }
                    } else {
                        ds~skip_bits(right_len);
                        (builders, loaded_bits) = cur_builder~save_bits(builders, loaded_bits, left, remained_bits, cell_max_bit);
                    }
                } else {
                    ds = ds~load_ref().begin_parse();
                }
            }
        }
    }



    builder value = builders~tpop();
    cell dc = value.end_cell();

    repeat (builders.tlen()) {
        value = builders~tpop();
        dc = value.store_ref(dc).end_cell();
    }

    return dc;

}

